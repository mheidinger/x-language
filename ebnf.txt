Startsymbol: Programm

Programm ::= NamenTeil
						 TeilSeparator
						 VariablenTeil
						 TeilSeparator
						 CodeTeil.
TeilSeparator ::= '===' {'='}.

NamenTeil ::= Zeichen {Zeichen}.

VariablenTeil ::= {Variable ';'}.
Variable ::= [ImportExportFlag] VariablenName VariablenTyp.
ImportExportFlag ::= ImportFlag | ExportFlag.
ImportFlag ::= '->'.
ExportFlag ::= '<-'.
VariablenName ::= Buchstabe {Zeichen}.
VariablenTyp ::= 'int' | 'float' | 'string'.

CodeTeil ::= {Instruktion}.
Instruktion ::= (Zuweisung ';') | Verzweigung | Schleife | Kommentar.
Kommentar ::= '//' {AlleZeichen}.

Zuweisung ::= VariablenName '=' EvaluationsWert.
EvaluationsWert ::= Wert
										| Addition
										| Subtraktion
										| Multiplikation
										| Division
										| RestDivision
										| '(' EvaluationsWert ')'.
Wert ::= VariablenName | Literal.
Literal ::= String | Int | Float.
Addition ::= EvaluationsWert '+' EvaluationsWert.
Subtraktion ::= EvaluationsWert '-' EvaluationsWert.
Multiplikation ::= EvaluationsWert '*' EvaluationsWert.
Division ::= EvaluationsWert '/' EvaluationsWert.
RestDivision ::= EvaluationsWert '%' EvaluationsWert.

Verzweigung ::= IfVerzweigung.
Schleife ::= WhileSchleife | ForSchleife.
InstruktionsBlock ::= '{' {Instruktion} '}'.

IfVerzweigung ::= 'if' Vergleich InstruktionsBlock
                  {ElseIfVerzweigung} [ElseVerzweigung].
Vergleich ::= GleichheitsVergleich
							| UngleichheitsVergleich
							| KleinerVergleich
							| GroesserVergleich.
GleichheitsVergleich ::= EvaluationsWert '==' EvaluationsWert.
UngleichheitsVergleich ::= EvaluationsWert '!=' EvaluationsWert.
KleinerVergleich ::= EvaluationsWert '<' EvaluationsWert.
GroesserVergleich ::= EvaluationsWert '>' EvaluationsWert.
ElseIfVerzweigung ::= 'else if' Vergleich InstruktionsBlock.
ElseVerzweigung ::= 'else' InstruktionsBlock.

WhileSchleife ::= 'while' Vergleich InstruktionsBlock.
ForSchleife ::= 'for' Zuweisung ';' Vergleich ';' Zuweisung InstruktionsBlock.

Zeichen := Buchstabe | Ziffer
Buchstabe ::= 'A'|...|'Z'|'a'|...|'z'.
AlleZeichen ::= Zeichen | Sonderzeichen.
Sonderzeichen ::= ' ' | '!' | ... | '@'.
Ziffer ::= '0'|...|'9'.
String ::= '"' {AlleZeichen} '"'.
Int ::= Ziffer {Ziffer}.
Float ::= Int '.' Int.
